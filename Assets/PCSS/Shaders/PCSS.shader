Shader "Hidden/PCSS"
{
    Properties
    {
    }

    HLSLINCLUDE

    #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
    #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"
    #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Shadows.hlsl"
    #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/DeclareDepthTexture.hlsl"
    #include "Packages/com.unity.render-pipelines.core/Runtime/Utilities/Blit.hlsl"

    // Configuration


    // Should receiver plane bias be used? This estimates receiver slope using derivatives,
    // and tries to tilt the PCF kernel along it. However, since we're doing it in screenspace
    // from the depth texture, the derivatives are wrong on edges or intersections of objects,
    // leading to possible shadow artifacts. So it's disabled by default.
    uniform float RECEIVER_PLANE_MIN_FRACTIONAL_ERROR = 0.025;

    // sizes of cascade projections, relative to first one
    float4 unity_ShadowCascadeScales;

    //
    // URP already declares _MainLightShadowmapTexture + sampler_MainLightShadowmapTexture
    // in Shadows.hlsl. We use those directly:
    //  - LOAD_TEXTURE2D for raw depth reads (blocker search)
    //  - SAMPLE_TEXTURE2D_SHADOW with URP's comparison sampler (PCF)
    //

    
    /**
    * Gets the cascade weights based on the world position of the fragment and the poisitions of the split spheres for each cascade.
    * Returns a float4 with only one component set that corresponds to the appropriate cascade.
    */
    inline half4 getCascadeWeights_splitSpheres(float3 wpos)
    {
        float3 fromCenter0 = wpos.xyz - _CascadeShadowSplitSpheres0.xyz;
        float3 fromCenter1 = wpos.xyz - _CascadeShadowSplitSpheres1.xyz;
        float3 fromCenter2 = wpos.xyz - _CascadeShadowSplitSpheres2.xyz;
        float3 fromCenter3 = wpos.xyz - _CascadeShadowSplitSpheres3.xyz;
        float4 distances2 = float4(dot(fromCenter0,fromCenter0), dot(fromCenter1,fromCenter1), dot(fromCenter2,fromCenter2), dot(fromCenter3,fromCenter3));
        half4 weights = float4(distances2 < _CascadeShadowSplitSphereRadii);
        weights.yzw = saturate(weights.yzw - weights.xyz);
        return weights;
    }

    /**
    * Returns the shadowmap coordinates for the given fragment based on the world position and z-depth.
    * These coordinates belong to the shadowmap atlas that contains the maps for all cascades.
    */
    inline float4 getShadowCoord( float4 wpos, half4 cascadeWeights )
    {
        float3 sc0 = mul (_MainLightWorldToShadow[0], wpos).xyz;
        float3 sc1 = mul (_MainLightWorldToShadow[1], wpos).xyz;
        float3 sc2 = mul (_MainLightWorldToShadow[2], wpos).xyz;
        float3 sc3 = mul (_MainLightWorldToShadow[3], wpos).xyz;
        float4 shadowMapCoordinate = float4(sc0 * cascadeWeights[0] + sc1 * cascadeWeights[1] + sc2 * cascadeWeights[2] + sc3 * cascadeWeights[3], 1);
        return shadowMapCoordinate;
    }

    /**
    * Computes the receiver plane depth bias for the given shadow coord in screen space.
    * Inspirations: 
    *		http://mynameismjp.wordpress.com/2013/09/10/shadow-maps/ 
    *		http://amd-dev.wpengine.netdna-cdn.com/wordpress/media/2012/10/Isidoro-ShadowMapping.pdf
    */
    float2 getReceiverPlaneDepthBias (float3 shadowCoord)
    {
        float2 biasUV;
        float3 dx = ddx (shadowCoord);
        float3 dy = ddy (shadowCoord);

        biasUV.x = dy.y * dx.z - dx.y * dy.z;
        biasUV.y = dx.x * dy.z - dy.x * dx.z;
        biasUV *= 1.0f / ((dx.x * dy.y) - (dx.y * dy.x));
        return biasUV;
    }

    /**
    * Reconstruct world position from depth buffer.
    * Uses URP's ComputeWorldSpacePosition (handles reversed-Z, Y-flip, all platforms).
    */
    inline float3 reconstructWorldPos(float2 uv)
    {
        float zdepth = SAMPLE_DEPTH_TEXTURE(_CameraDepthTexture, sampler_CameraDepthTexture, uv);
        return ComputeWorldSpacePosition(uv, zdepth, UNITY_MATRIX_I_VP);
    }

    /**
    * Reconstruct view-space position from depth buffer.
    */
    inline float3 reconstructViewPos(float2 uv)
    {
        float3 worldPos = reconstructWorldPos(uv);
        return mul(UNITY_MATRIX_V, float4(worldPos, 1.0)).xyz;
    }

    

    //PCSS --------------------------------------------------------------------------------------------------------------------------------------------------------------
    //-------------------------------------------------------------------------------------------------------------------------------------------------------------------
    //-------------------------------------------------------------------------------------------------------------------------------------------------------------------
    //-------------------------------------------------------------------------------------------------------------------------------------------------------------------

    uniform float Blocker_Samples = 32;
    uniform float PCF_Samples = 32;

    uniform float Blocker_Rotation = .5;
    uniform float PCF_Rotation = .5;

    uniform float Softness = 1.0;
    uniform float SoftnessFalloff = 1.0;
    //uniform float NearPlane = .1;

    uniform float Blocker_GradientBias = 0.0;
    uniform float PCF_GradientBias = 1.0;
    uniform float CascadeBlendDistance = .5;

    uniform float PenumbraWithMaxSamples = .15;

    TEXTURE2D(_NoiseTexture);
    SAMPLER(sampler_NoiseTexture);
    uniform float4 NoiseCoords;

    #if defined(POISSON_32)
        static const float2 PoissonOffsets[32] = {
            float2(0.06407013, 0.05409927),
            float2(0.7366577, 0.5789394),
            float2(-0.6270542, -0.5320278),
            float2(-0.4096107, 0.8411095),
            float2(0.6849564, -0.4990818),
            float2(-0.874181, -0.04579735),
            float2(0.9989998, 0.0009880066),
            float2(-0.004920578, -0.9151649),
            float2(0.1805763, 0.9747483),
            float2(-0.2138451, 0.2635818),
            float2(0.109845, 0.3884785),
            float2(0.06876755, -0.3581074),
            float2(0.374073, -0.7661266),
            float2(0.3079132, -0.1216763),
            float2(-0.3794335, -0.8271583),
            float2(-0.203878, -0.07715034),
            float2(0.5912697, 0.1469799),
            float2(-0.88069, 0.3031784),
            float2(0.5040108, 0.8283722),
            float2(-0.5844124, 0.5494877),
            float2(0.6017799, -0.1726654),
            float2(-0.5554981, 0.1559997),
            float2(-0.3016369, -0.3900928),
            float2(-0.5550632, -0.1723762),
            float2(0.925029, 0.2995041),
            float2(-0.2473137, 0.5538505),
            float2(0.9183037, -0.2862392),
            float2(0.2469421, 0.6718712),
            float2(0.3916397, -0.4328209),
            float2(-0.03576927, -0.6220032),
            float2(-0.04661255, 0.7995201),
            float2(0.4402924, 0.3640312),
        };

    #else
        static const float2 PoissonOffsets[64] = {
            float2(0.0617981, 0.07294159),
            float2(0.6470215, 0.7474022),
            float2(-0.5987766, -0.7512833),
            float2(-0.693034, 0.6913887),
            float2(0.6987045, -0.6843052),
            float2(-0.9402866, 0.04474335),
            float2(0.8934509, 0.07369385),
            float2(0.1592735, -0.9686295),
            float2(-0.05664673, 0.995282),
            float2(-0.1203411, -0.1301079),
            float2(0.1741608, -0.1682285),
            float2(-0.09369049, 0.3196758),
            float2(0.185363, 0.3213367),
            float2(-0.1493771, -0.3147511),
            float2(0.4452095, 0.2580113),
            float2(-0.1080467, -0.5329178),
            float2(0.1604507, 0.5460774),
            float2(-0.4037193, -0.2611179),
            float2(0.5947998, -0.2146744),
            float2(0.3276062, 0.9244621),
            float2(-0.6518704, -0.2503952),
            float2(-0.3580975, 0.2806469),
            float2(0.8587891, 0.4838005),
            float2(-0.1596546, -0.8791054),
            float2(-0.3096867, 0.5588146),
            float2(-0.5128918, 0.1448544),
            float2(0.8581337, -0.424046),
            float2(0.1562584, -0.5610626),
            float2(-0.7647934, 0.2709858),
            float2(-0.3090832, 0.9020988),
            float2(0.3935608, 0.4609676),
            float2(0.3929337, -0.5010948),
            float2(-0.8682281, -0.1990303),
            float2(-0.01973724, 0.6478714),
            float2(-0.3897587, -0.4665619),
            float2(-0.7416366, -0.4377831),
            float2(-0.5523247, 0.4272514),
            float2(-0.5325066, 0.8410385),
            float2(0.3085465, -0.7842533),
            float2(0.8400612, -0.200119),
            float2(0.6632416, 0.3067062),
            float2(-0.4462856, -0.04265022),
            float2(0.06892014, 0.812484),
            float2(0.5149567, -0.7502338),
            float2(0.6464897, -0.4666451),
            float2(-0.159861, 0.1038342),
            float2(0.6455986, 0.04419327),
            float2(-0.7445076, 0.5035095),
            float2(0.9430245, 0.3139912),
            float2(0.0349884, -0.7968109),
            float2(-0.9517487, 0.2963554),
            float2(-0.7304786, -0.01006928),
            float2(-0.5862702, -0.5531025),
            float2(0.3029106, 0.09497032),
            float2(0.09025345, -0.3503742),
            float2(0.4356628, -0.0710125),
            float2(0.4112572, 0.7500054),
            float2(0.3401214, -0.3047142),
            float2(-0.2192158, -0.6911137),
            float2(-0.4676369, 0.6570358),
            float2(0.6295372, 0.5629555),
            float2(0.1253822, 0.9892166),
            float2(-0.1154335, 0.8248222),
            float2(-0.4230408, -0.7129914),
        };
    #endif

    /*
    =========================================================================================================================================
    ++++++++++++++++++++++++++++++++++++++++++++++++++++++    Helper Methods    +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    =========================================================================================================================================
    */

    inline float ValueNoise(float3 pos)
    {
        float3 Noise_skew = pos + 0.2127 + pos.x * pos.y * pos.z * 0.3713;
        float3 Noise_rnd = 4.789 * sin(489.123 * (Noise_skew));
        return frac(Noise_rnd.x * Noise_rnd.y * Noise_rnd.z * (1.0 + Noise_skew.x));
    }

    inline float2 Rotate(float2 pos, float2 rotationTrig)
    {
        return float2(pos.x * rotationTrig.x - pos.y * rotationTrig.y, pos.y * rotationTrig.x + pos.x * rotationTrig.y);
    }

    inline float SampleShadowmapDepth(float2 uv)
    {
        // Load raw depth from URP's shadow atlas (bypasses comparison sampler)
        int2 texCoord = int2(uv * _MainLightShadowmapSize.zw);
        return LOAD_TEXTURE2D(_MainLightShadowmapTexture, texCoord).r;
    }

    inline float SampleShadowmap_Soft(float4 coord)
    {
        return SAMPLE_TEXTURE2D_SHADOW(_MainLightShadowmapTexture, sampler_MainLightShadowmapTexture, coord.xyz);
    }

    inline float SampleShadowmap(float4 coord)
    {
        float depth = SampleShadowmapDepth(coord.xy);
        return step(depth, coord.z);
    }

    inline float GetScale(float4 cascadeWeights)
    {
        float scale = 1.0;
        scale = (cascadeWeights.y > 0.0) ? 2.0 : scale;
        scale = (cascadeWeights.z > 0.0) ? 4.0 : scale;
        scale = (cascadeWeights.w > 0.0) ? 8.0 : scale;
        return 1.0 / scale;
    }

    /*
    =========================================================================================================================================
    ++++++++++++++++++++++++++++++++++++++++++++++++++++++    Find Blocker    +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    =========================================================================================================================================
    */

    float2 FindBlocker(float2 uv, float depth, float scale, float searchUV, float2 receiverPlaneDepthBias, float2 rotationTrig)
    {
        float avgBlockerDepth = 0.0;
        float numBlockers = 0.0;
        float blockerSum = 0.0;

        for (int i = 0; i < Blocker_Samples; i++)
        {
            float2 offset = PoissonOffsets[i] * searchUV * scale;

            //#if defined(ROTATE_SAMPLES)
            offset = Rotate(offset, rotationTrig);
            //#endif

            float shadowMapDepth = SampleShadowmapDepth(uv + offset);

            float biasedDepth = depth;

            #if defined(USE_BLOCKER_BIAS)
                biasedDepth += dot(offset, receiverPlaneDepthBias) * Blocker_GradientBias;
            #endif

            // URP shadow maps use forward-Z (closer = smaller depth)
            if (shadowMapDepth < biasedDepth)
            {
                blockerSum += shadowMapDepth;
                numBlockers += 1.0;
            }
        }

        avgBlockerDepth = blockerSum / numBlockers;

        return float2(avgBlockerDepth, numBlockers);
    }

    /*
    =========================================================================================================================================
    ++++++++++++++++++++++++++++++++++++++++++++++++++++++    PCF Sampling    +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    =========================================================================================================================================
    */

    float PCF_Filter(float2 uv, float depth, float scale, float filterRadiusUV, float2 receiverPlaneDepthBias, float penumbra, float2 rotationTrig)
    {
        float sum = 0.0f;

        //float penumbraPercent = saturate(penumbra / PenumbraWithMaxSamples);
        //int samples = ceil(penumbraPercent * PCF_Samples);
        ////int samples = ceil((1.0 - (penumbraPercent * penumbraPercent)) * PCF_Samples);
        //samples = PCF_Samples;


        //for (int i = 0; i < samples; i++)
        for (int i = 0; i < PCF_Samples; i++)
        {
            float2 offset = PoissonOffsets[i] * filterRadiusUV * scale;

            //#if defined(ROTATE_SAMPLES)
            offset = Rotate(offset, rotationTrig);
            //#endif

            float biasedDepth = depth;

            #if defined(USE_PCF_BIAS)
                biasedDepth += dot(offset, receiverPlaneDepthBias) * PCF_GradientBias;
            #endif

            float value = SampleShadowmap_Soft(float4(uv.xy + offset, biasedDepth, 0));

            sum += value;
        }

        //sum /= samples;
        sum /= PCF_Samples;

        return sum;
    }


    /*
    =========================================================================================================================================
    ++++++++++++++++++++++++++++++++++++++++++++++++++++++++    PCSS Main    ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    =========================================================================================================================================
    */

    float PCSS_Main(float4 coords, float2 receiverPlaneDepthBias, float random, float scale)
    {
        float2 uv = coords.xy;
        float depth = coords.z;
        // URP shadow maps always use forward-Z (closer = smaller depth)
        float zAwareDepth = depth;

        //float rotationAngle = random * 6.283185307179586476925286766559;
        float rotationAngle = random * 3.1415926;
        float2 rotationTrig = float2(cos(rotationAngle), sin(rotationAngle));

        // STEP 1: blocker search
        //float searchSize = Softness * (depth - _LightShadowData.w) / depth;
        float searchSize = Softness * saturate(zAwareDepth - .02) / zAwareDepth;
        float2 blockerInfo = FindBlocker(uv, depth, scale, searchSize, receiverPlaneDepthBias, rotationTrig);

        if (blockerInfo.y < 1)
        {
            //There are no occluders so early out (this saves filtering)
            return 1.0;
        }

        // STEP 2: penumbra size
        //float penumbra = zAwareDepth * zAwareDepth - blockerInfo.x * blockerInfo.x;
        float penumbra = zAwareDepth - blockerInfo.x;

        #if defined(USE_FALLOFF)
            penumbra = 1.0 - pow(1.0 - penumbra, SoftnessFalloff);
        #endif

        float filterRadiusUV = penumbra * Softness;
        //filterRadiusUV *= filterRadiusUV;

        // URP: _MainLightShadowParams.x = shadowStrength (NOT 1-strength)
        float shadow = PCF_Filter(uv, depth, scale, filterRadiusUV, receiverPlaneDepthBias, penumbra, rotationTrig);
        return lerp(1.0 - _MainLightShadowParams.x, 1.0, shadow);
    }

    
    //END PCSS ----------------------------------------------------------------------------------------------------------------------------------------------------------
    //-------------------------------------------------------------------------------------------------------------------------------------------------------------------
    //-------------------------------------------------------------------------------------------------------------------------------------------------------------------
    //-------------------------------------------------------------------------------------------------------------------------------------------------------------------

    /**
    *	Hard shadow 
    */
    half4 frag_hard (Varyings input) : SV_Target
    {
        UNITY_SETUP_STEREO_EYE_INDEX_POST_VERTEX(input);
        float2 uv = input.texcoord;

        float3 wpos3 = reconstructWorldPos(uv);
        float4 shadowCoord = TransformWorldToShadowCoord(wpos3);
        half shadow = SAMPLE_TEXTURE2D_SHADOW(_MainLightShadowmapTexture, sampler_MainLightShadowmapTexture, shadowCoord.xyz);

        // URP: _MainLightShadowParams.x = shadowStrength (NOT 1-strength)
        return lerp(1.0 - _MainLightShadowParams.x, 1.0, shadow);
    }

    
    /**
    *	Soft Shadow Frag (PCSS)
    */
    half4 frag_pcss (Varyings input) : SV_Target
    {
        UNITY_SETUP_STEREO_EYE_INDEX_POST_VERTEX(input);
        float2 uv = input.texcoord;

        float3 wpos3 = reconstructWorldPos(uv);
        float4 wpos = float4(wpos3, 1.0);

        // Use URP's built-in cascade selection and shadow coordinate computation
        half4 cascadeWeights = getCascadeWeights_splitSpheres(wpos3);
        float4 coord = TransformWorldToShadowCoord(wpos3);

        // Skip bias entirely — test clean PCSS
        float scale = GetScale(cascadeWeights);
        float shadow = PCSS_Main(coord, float2(0,0), 0.0, scale);

        return shadow;
    }
    ENDHLSL

    // ----------------------------------------------------------------------------------------
    // SubShader for URP PCSS soft shadows (primary)
    SubShader
    {
        Tags { "RenderPipeline" = "UniversalPipeline" }
        
        // Pass 0: PCSS Soft Shadows
        Pass
        {
            Name "PCSS_SOFT"
            ZWrite Off ZTest Always Cull Off

            HLSLPROGRAM
            #pragma vertex Vert
            #pragma fragment frag_pcss
            #pragma multi_compile _ _MAIN_LIGHT_SHADOWS _MAIN_LIGHT_SHADOWS_CASCADE
            #pragma multi_compile _ USE_FALLOFF
            #pragma multi_compile _ USE_STATIC_BIAS
            #pragma multi_compile _ USE_BLOCKER_BIAS
            #pragma multi_compile _ USE_PCF_BIAS
            #pragma multi_compile POISSON_32 POISSON_64
            ENDHLSL
        }

        // Pass 1: Hard Shadows (fallback)
        Pass
        {
            Name "PCSS_HARD"
            ZWrite Off ZTest Always Cull Off

            HLSLPROGRAM
            #pragma vertex Vert
            #pragma fragment frag_hard
            #pragma multi_compile _ _MAIN_LIGHT_SHADOWS _MAIN_LIGHT_SHADOWS_CASCADE
            ENDHLSL
        }
    }
}